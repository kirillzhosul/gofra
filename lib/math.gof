// ==============================================
// Math helpers
// E.g power, min/max, clamp, factorials
// ==============================================

// TODO: Gofra does not have FP so this is only integer-math library

// Calculate base in power of exponent
func int pow[int base, int exponent]
    var result int = 1;

    // TODO: This is not performant way to calculate power, but this is enough for now
    while exponent 0 > do
        &result result base * !<
        &exponent exponent 1 - !<
    end

    result return
end

// Calculate integer square root for an value (e.g floor Babylonian method)
func int sqrt[int n]
    var y int = 1;
    var x int;

    // TODO: X64/AARCH64 has direct machine ops to perform FSQRT, probably at some time we introduce injected SQRT when done with FPU/FP and need fast SQRT (e.g 3D renderers)
    &x n !< // x = n

    while x y > do 
        &x x y + 2 / !<
        &y n x /     !<
    end

    x return
end

// Return absolute value of an negative/positive integer
func int abs[int x]
    x 0 < if 
        x -1 * return // (-n) * -1 => n
    end
    x return // Positive by default
end

// Return minimum value from two values
func int min[int a, int b]
    a b <= if a return end
    b return
end

// Return maximals value from two values
func int max[int a, int b]
    a b >= if a return end
    b return
end

// Clamp given value between two constraints
func int clamp[int n, int a, int b]
    n a < if a return end
    n b > if b return end
    n return
end

// Factorial (n!) of value
func int factorial[int n]
    var r = 1;
    var i = 2;

    while i n <= do
        &r r i * !<
        &i i 1 + !<
    end

    r return
end
