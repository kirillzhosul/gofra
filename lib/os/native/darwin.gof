// ==============================================
// Darwin native system interaction layer and constant
// Must works on MacOS, iOS, watchOS and other Darwin kernel operating system
// Interacts with kernel via system/supervisor calls
// ==============================================

// TODO: Add x86-64 Darwin layer
// TODO: Add variadic function for `sc_syscall`

#include "types.gof"

// File access mode flags
#define O_RDONLY   0x0000  // Open for reading only
#define O_WRONLY   0x0001  // Open for writing only
#define O_RDWR     0x0002  // Open for reading and writing O_RDONLY | O_WRONLY

// Standard file descriptors
#define STD_IN_FD  0
#define STD_OUT_FD 1
#define STD_ERR_FD 2

#define OS_LINESEP "\n" // Line separator

// Network socket address in structure
type struct sockaddr_in
    sin_family char[2]
    sin_port   char[2]
    sin_addr   char[4]
    sin_zero   int   // zero padding
end

type struct timeval
    tv_sec int
    tv_usec int
end

type timezone char[16]
type sc_argvp **char[]
type sc_envp **char[]
type fd_t int

// --- Syscall Numbers (Partial Table) ---
#ifdef ARCH_ARM64
    #define DARWIN_SYSCALL_EXIT         1
    #define DARWIN_SYSCALL_FORK         2
    #define DARWIN_SYSCALL_READ         3
    #define DARWIN_SYSCALL_WRITE        4
    #define DARWIN_SYSCALL_OPEN         5
    #define DARWIN_SYSCALL_CLOSE        6
    #define DARWIN_SYSCALL_GETPID       20
    #define DARWIN_SYSCALL_ACCEPT       30
    #define DARWIN_SYSCALL_GETPPID      39
    #define DARWIN_SYSCALL_EXECVE       59
    #define DARWIN_SYSCALL_SELECT       93
    #define DARWIN_SYSCALL_SOCKET       97
    #define DARWIN_SYSCALL_CONNECT      98
    #define DARWIN_SYSCALL_BIND         104
    #define DARWIN_SYSCALL_LISTEN       106
    #define DARWIN_SYSCALL_GETTIMEOFDAY 116
    #define DARWIN_SYSCALL_MMAP         197
#endif // ARCH_ARM64


// ---  Syscall Wrappers ---
// These are the preferred way to make syscalls.
#ifdef ARCH_ARM64
    // Terminates the current process. Does not return.
    no_return func void sc_exit[int exit_code] exit_code DARWIN_SYSCALL_EXIT syscall1 drop end

    // Reads from a file descriptor.
    // fd: file descriptor, buf: buffer pointer, count: number of bytes to read
    // Returns number of bytes read, 0 on EOF, or -1 on error.
    func int sc_read[fd_t fd, CStr buf, int count] fd buf count DARWIN_SYSCALL_READ syscall3 end

    // Writes to a file descriptor.
    // fd: file descriptor, buf: buffer pointer, count: number of bytes to write
    // Returns number of bytes written, or -1 on error.
    func int sc_write[fd_t fd, CStr buf, int count] fd buf count DARWIN_SYSCALL_WRITE syscall3 end

    // Opens a file.
    // path: pathname pointer, flags: open flags (O_RDONLY, etc.), mode: file permissions (if O_CREAT)
    // Returns file descriptor, or -1 on error.
    func int sc_open[CStr path, int flags, int mode] path flags mode DARWIN_SYSCALL_OPEN syscall3 end

    // Closes a file descriptor.
    func int sc_close[fd_t fd] fd DARWIN_SYSCALL_CLOSE syscall1 end

    // Creates a new process. Returns 0 to child, PID to parent, or -1 on error.
    func int sc_fork[] DARWIN_SYSCALL_FORK syscall0 end

    // Get PID of current process
    func int sc_getpid[] DARWIN_SYSCALL_GETPID syscall0 end

    // Get PID of parent process
    func int sc_getppid[] DARWIN_SYSCALL_GETPPID syscall0 end

    // Abort current process execution (process suicide) and reincarnation as specified executable
    // Does not returns anything unless no errors occurred, otherwise always returns -1
    func int sc_execve[CStr pathname, sc_argvp argv, sc_envp envp] pathname argv envp DARWIN_SYSCALL_EXECVE syscall3 end

    // Accept pending connections on given socket and return client socket
    func int sc_accept[int socket, *sockaddr_in address, int address_len] socket address address_len DARWIN_SYSCALL_ACCEPT syscall3 end

    // Creates an socket FD
    // [domain,type,protocol]
    func fd_t sc_socket[int,int,int] DARWIN_SYSCALL_SOCKET syscall3 end

    // Bind given socket to given address
    func int sc_bind[fd_t, *sockaddr_in, int] DARWIN_SYSCALL_BIND syscall3 end

    // Listen to an socket with specified backlog
    func int sc_listen[fd_t, int] DARWIN_SYSCALL_LISTEN syscall2 end

    // Get current time of day
    // Notice that timezone usage is discouraged so pass NULL always
    func void sc_gettimeofday[*timeval tv, *timezone tz] tv tz DARWIN_SYSCALL_GETTIMEOFDAY syscall2 drop end

    // Await given descriptors to be ready
    // Signature: [nfds, readfds, writefds, exceptfds, timeout]
    func int sc_select[int nfds, *void readfds, *void writefds, *void exceptfds, *timeval timeout]
        nfds readfds writefds exceptfds timeout DARWIN_SYSCALL_SELECT syscall5 
    end

    // Map memory (allocate it)
    // Signature: [address, size, prot, flags, fd, offset]
    // TODO!: Return at the end as parser/lexer eats some identifiers...
    func *void sc_mmap[int address, int size, int prot, int flags, int fd , int offset] 
        address size prot flags fd offset DARWIN_SYSCALL_MMAP syscall6 
        typecast *void return 
    end
#endif // ARCH_ARM64