// Complex example of an using FFI-library (Raylib, C) as an graphics wrapper
// Opens an window with a 2D-square which is can be moved with WASD.

// Requires additional CLI toolchain flags to link with raylib
// MacOS: `-L=-lraylib -L=-L/opt/homebrew/lib`

#include "ext/raylib.gof"
#include "std.gof"

memory player_x 4
memory player_y 4

#define BACKGROUND_COLOR 0xFFFFFFFF

#define PLAYER_COLOR 0xFF0000FF
#define PLAYER_SIZE  64
#define PLAYER_SPEED 5

#define WINDOW_NAME   "Welcome to Gofra!"
#define WINDOW_WIDTH  512
#define WINDOW_HEIGHT 512  
#define WINDOW_FPS    60


inline func void init_window[]
    // Init window with Raylib
    WINDOW_WIDTH
    WINDOW_HEIGHT 
    WINDOW_NAME to_cstr_ptr 
    call InitWindow

    WINDOW_FPS
    call SetTargetFPS
end

inline func void draw_player[]
    // Draws an player where it is located
    player_x ?>
    player_y ?>
    PLAYER_SIZE 
    PLAYER_SIZE
    PLAYER_COLOR
    call DrawRectangle
end


func void main[]
    // Default player position
    player_x 120 !<
    player_y 120 !<

    call init_window

    while call WindowShouldClose 0 == do 
        call BeginDrawing
            call draw_player

            BACKGROUND_COLOR
            call ClearBackground

            'W' call IsKeyDown if
                player_y copy ?> PLAYER_SPEED - !<
            end
            'S' call IsKeyDown if
                player_y copy ?> PLAYER_SPEED + !<
            end 
            'A' call IsKeyDown if
                player_x copy ?> PLAYER_SPEED - !<
            end 
            'D' call IsKeyDown if
                player_x copy ?> PLAYER_SPEED + !<
            end
        call EndDrawing
    end

    call CloseWindow
    0 call exit
end