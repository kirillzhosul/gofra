
// ==============================================
// Fibonacci sequence
// Print an series of fibonacci numbers up to N
// ==============================================

#include "std.gof"

#define N 1024
var buf char[128]

func void print_int_with_sep[int]
    buf int2str
    buf sizeof buf print
    ", " print
end

func void main[]
    var a int
    var b int
    var x int

    a 0 !<
    b 1 !<
    x a ?> b ?> + !<

    "Fibonacci Series: " print
    a ?> print_int_with_sep
    b ?> print_int_with_sep

    while x ?> N <= do
        x ?> buf int2str 
        buf sizeof buf print

        a b ?> !<
        b x ?> !<
        x a ?> b ?> + !<

        x ?> N <= if
            ", " print
        end
    end 

    "\n" print
end